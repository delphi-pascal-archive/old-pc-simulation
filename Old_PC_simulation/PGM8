010 GOSUB 810
015 ? SPC(9) "‡*** TAKE-DOWN GAME ***"
020 FOR NB = 1 TO NBP
030 S = RND(8)+1
032 TN$ = TN$+STR$(S)
034 NBB = NBB+S
040 NEXT NB
050 NB = 1
055 TB = 15
060 P$ = MID$(TN$,NB,1)
065 P = VAL(P$)
070 GOSUB 910
080 ? TAB(TB) " "
085 ? TAB(TB) NB
090 NB = NB+1
100 TB = TB+2
105 IF NB <= NBP GOTO 060
110 IF JR = 2 GOTO 300
120 NL = 17
122 GOSUB 600
125 ? " * A VOUS DE JOUER..."
130 ? "‡ * QUELLE PILE ? ";
135 GET PL
136 ? PL
140 ? "‡ * NBRE DE PIERRES: ";
145 GET NP
146 ? NP
150 P$ = MID$(TN$,PL,1)
155 P = VAL(P$)-NP
160 REM P = P - NP
170 GOSUB 710
180 TB = PL*2+13
190 GOSUB 910
200 GOSUB 760
250 NBB = NBB-NP
260 IF NBB > 0 GOTO 300
262 GOSUB 760
264 NL = 17
266 GOSUB 600
270 ? "†††††* BRAVO, VOUS AVEZ GAGNE *"
280 END
300 REM Jeu MZ ------------------------
310 NL = 17
315 GOSUB 600
320 ? " * A MON TOUR..."
322 WAIT 10
325 P = 0
330 FOR I = 1 TO NBP
335 P$ = MID$(TN$,I,1)
340 P1 = VAL(P$)
345 IF P1 <= P GOTO 360
350 P = P1
355 PL = I
360 NEXT I
365 IF P > 1 GOTO 380
370 NP = 1
375 GOTO 385
380 NP = RND(P)+1
385 P = P-NP
390 GOSUB 700
395 TB = PL*2+13
398 ? "‡ * JE JOUE: PILE " PL ",";
399 ? NP " PIERRES"
400 WAIT 20
402 GOSUB 900
405 GOSUB 760
410 NBB = NBB-NP
415 IF NBB > 0 GOTO 120
420 ? "      ET JE GAGNE !!!"
590 END
600 REM Position ligne ----------------
610 ? "æ";
620 FOR L = 1 TO NL
625 ? TAB(1)
630 NEXT L
635 RETURN 
700 REM Formatage tableau Piles -------
710 T$ = ""
712 IF PL = 1 GOTO 725
715 L1 = PL-1
720 T$ = LEFT$(TN$,L1)
725 T$ = T$+STR$(P)
730 IF PL = NBP GOTO 745
735 L1 = NBP-PL
740 T$ = T$+RIGHT$(TN$,L1)
745 TN$ = T$
750 RETURN
760 REM Efface texte ------------------
765 ? "æ‡‡‡‡‡‡‡‡‡‡‡‡‡‡‡‡"
770 ? SPC(30)
775 ? "‡" SPC(30)
780 ? "‡" SPC(30)
785 RETURN
800 REM Page de debut -----------------
810 ? SPC(9) "‡‡*** TAKE-DOWN GAME ***"
815 ? "‡‡  * COMMENT JOUER ?"
820 ? "‡  VOUS ET MOI ALLONS PRENDRE ";
825 ? "A TOUR DE"
830 ? "‡  UNE PIERRE SUR UNE DES PILES"
835 ? "‡  LE VAINQUEUR EST CELUI QUI ";
840 ? "RAMASSE"
845 ? "‡  LA DERNIERE PIERRE."
850 ? "‡  LE NOMBRE MAXI DE PILES EST";
855 ? " DE 8."
860 ? "‡‡‡  * COMBIEN DE PILES (3-8)? ";
865 GET NBP
866 ? NBP
870 ? "‡‡  * VOULEZ-COMMENCER (O-N)? ";
872 GET R$
873 ? R$
874 JR = 1
875 IF R$ = "N" THEN JR = 2
880 CLS
890 RETURN
900 REM Affiche pile -------------------
910 ? "æ‡‡‡‡"
920 FOR N = 8 TO 1 STEP -1
930 IF N > P GOTO 960
940 ? TAB(TB) "Œ"
950 GOTO 970
960 ? TAB(TB) " "
970 NEXT N
980 RETURN
